//file:noinspection DependencyNotationArgument

plugins {
    id "io.spring.dependency-management" version "1.1.0"
}

group "com.github.markostijak.jarvis"

ext {
    // spring boot
    springBootVersion = "3.4.5"
    // jetbrains annotations
    jbAnnotationsVersion = "26.0.2"
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"

    ext {
        published = false
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://packages.confluent.io/maven"
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/markostijak/jarvis")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                artifactId project.name
                from(components.java)
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        }
    }

    dependencies {
        compileOnly group: "org.projectlombok", name: "lombok"
        annotationProcessor group: "org.projectlombok", name: "lombok"

        annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor"
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    test {
        useJUnitPlatform()
    }

    group = 'com.github.markostijak.jarvis'
    version "1.0.0"

    publishGprPublicationToMavenLocal.onlyIf { project.published }
    publishGprPublicationToGitHubPackagesRepository.onlyIf { project.published }
}
